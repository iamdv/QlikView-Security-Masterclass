///$tab Main
SET ThousandSep=',';
SET DecimalSep='.';
SET MoneyThousandSep=',';
SET MoneyDecimalSep='.';
SET MoneyFormat='£#,##0.00;-£#,##0.00';
SET TimeFormat='hh:mm:ss';
SET DateFormat='DD-MMM-YYYY';
SET TimestampFormat='DD-MMM-YYYY hh:mm:ss[.fff]';
SET FirstWeekDay=0;
SET BrokenWeeks=0;
SET ReferenceDay=4;
SET FirstMonthOfYear=1;
SET CollationLocale='en-GB';
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET LongMonthNames='January;February;March;April;May;June;July;August;September;October;November;December';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';

SET HidePrefix = '%';

/**********************************************
You need to change the Root Path on your machine.
Rest of the script should work without any changes.
Make sure you have the forward slash at the end of the path
**********************************************/

SET vRootPath = 'C:\Development\';

/**********************************************/
///$tab Config
///$tab MAPPING TABLES
Map_Suppliers:
Mapping LOAD 
	[Supplier ID]							AS		%Key_SupplierID,		
	Upper([Supplier Name])					AS		%Value_SupplierName
FROM [$(vRootPath)QlikView Security Masterclass\QVD\Supplier.qvd] (qvd);



Map_ProductsSuppliers:
Mapping LOAD 
	[Product ID]							AS		%Key_ProductID,	

	ApplyMap('Map_Suppliers', [Supplier ID], '<Unknown>')
										AS		%Value_SuppliersProductsName
FROM [$(vRootPath)QlikView Security Masterclass\QVD\Products.qvd] (qvd);



Map_CustomersCountry:
Mapping LOAD 
	[Customer ID]							AS		%Key_CustomerID,		 
	Upper(Country)						AS		%Value_Country
FROM [$(vRootPath)QlikView Security Masterclass\QVD\Customers.qvd] (qvd);


Map_Nulls:
Mapping LOAD 
	Null()							AS		%Key_NullValue,		 
	'<Unknown>'						AS		%Value_Unknown
AutoGenerate 1;
///$tab DIm - Customers
Customers:
LOAD 
	[Customer ID]						AS		%Key_CustomerID,		 
	Address1, 
	Address2, 
	City, 
	[Contact First Name], 
	[Contact Last Name], 
	[Contact Position], 
	[Contact Title], 
	Country,
	ApplyMap('Map_Nulls', Upper(Country), Upper(Country))					AS		%COUNTRY,	
	[Customer Credit ID], 
	[Customer ID], 
	[Customer Name], 
	[E-mail], 
	Fax, 
	[Last Year's Sales], 
	Phone, 
	[Postal Code], 
	Region, 
	[Web Site]
FROM [$(vRootPath)QlikView Security Masterclass\QVD\Customers.qvd] (qvd);


///$tab Dim - Employee
Employees:
LOAD 
	[Employee ID]						AS		%Key_EmployeeID,		 
	[Birth Date], 
	[Emergency Contact First Name], 
	[Emergency Contact Last Name], 
	[Emergency Contact Phone], 
	[Emergency Contact Relationship], 
	[Employee ID], 
	Extension, 
	[First Name], 
	[Hire Date], 
	HireYear, 
	[Last Date],
	[Home Phone], 
	[Last Name],
	[Employee Name], 
	Notes, 
	Photo, 
	Position, 
	[Reports To], 
	Salary, 
	SSN, 
	[Supervisor ID], 
	FireYear
FROM [$(vRootPath)QlikView Security Masterclass\QVD\Employees.qvd] (qvd);
///$tab Dim - Employee Address
EmployeeAddress:
LOAD 
	[Employee ID]						AS		%Key_EmployeeID,	
	[Employee City], 
	[Employee Country], 
	[Employee Postal Code], 
	[Employee Region]
FROM [$(vRootPath)QlikView Security Masterclass\QVD\EmployeeAddress.qvd] (qvd);

///$tab Dim - Teams
Teams:
LOAD 
	[Employee ID]					AS		%Key_EmployeeID,	 
	[Employee Name1], 
	[Employee Name2], 
	[Employee Name3], 
	Manager, 
	[Employee Hierarchy], 
	[Reports To]						AS		[Reports To (Team)], 
	[Employee Name]					AS		[Employee Name (Team)]
FROM [$(vRootPath)QlikView Security Masterclass\QVD\Teams.qvd] (qvd);

///$tab Dim - Calendar
Calendar:
LOAD 
	Floor([Order Date])								AS		%Key_OrderDate,	
	[Order Date], 
	Year, 
	QtrYear, 
	Week, 
	Weekday, 
	Weekstart, 
	Day, 
	DateMMDD, 
	CY, 
	FPY, 
	SPY, 
	CYTD, 
	FPYTD, 
	SPYTD, 
	CQ, 
	FPQ, 
	SPQ, 
	CQTD, 
	FPQTD, 
	SPQTD, 
	CM, 
	PM, 
	FPM, 
	SPM, 
	CMTD, 
	PMTD, 
	FPMTD, 
	SPMTD, 
	CW, 
	FPW, 
	SPW, 
	CWTD, 
	FPWTD,
	SPWTD, 
	CD, 
	FPD, 
	SPD, 
	W1, 
	W2, 
	W3, 
	W4, 
	W1TD, 
	W2TD, 
	W3TD, 
	W4TD
FROM [$(vRootPath)QlikView Security Masterclass\QVD\MasterCalendar.qvd] (qvd);

///$tab Dim - Products
Products:
LOAD 
	[Product ID]							AS		%Key_ProductID,	
	[Product Type ID]						AS		%Key_ProductTypeID,		
	[Supplier ID]							AS		%Key_SupplierID,
	ApplyMap('Map_Suppliers', [Supplier ID], '<Unknown>')
										AS		[Supplier Name],
	Color, 
	[M/F], 
	[Product Class],
	[Product ID], 
	[Product Name], 
	[Product Type ID], 
	Size, 
	[Supplier ID]
FROM [$(vRootPath)QlikView Security Masterclass\QVD\Products.qvd] (qvd);


Map_ProductID_SupplierName:
Mapping LOAD Distinct
	%Key_ProductID,	
	[Supplier Name]
Resident Products;

Map_ProductID_ProductName:
Mapping LOAD Distinct
	%Key_ProductID,	
	[Product Name]
Resident Products;
///$tab Dim - Product Description
ProductDescription:
LOAD 
	[Product Type ID]						AS		%Key_ProductTypeID,	
	Description, 
	[Graphic Image Location], 
	Picture, 
	[Product Type Name]
FROM [$(vRootPath)QlikView Security Masterclass\QVD\ProductType.qvd] (qvd);

///$tab FACT - Orders
Orders:
LOAD *,
Upper(%CustomerCountry) & ' | ' & Upper([Supplier Name (Fact)]) & ' | ' & Upper([Product Name (Fact)])
										AS		%Key_FactAuth
;


LOAD 

	//[Product ID] & ' | ' & [Customer ID]		AS		%Auth_Key_ProductCustomerID,	
										
	[Customer ID]							AS		%Key_CustomerID,		
	[Product ID]							AS		%Key_ProductID,		 
	%Date								AS		%Key_OrderDate,	 	
	[Employee ID]							AS		%Key_EmployeeID,	

	ApplyMap('Map_CustomersCountry', [Customer ID], '<Unknown>')
										AS		%CustomerCountry,
										
	ApplyMap('Map_ProductID_SupplierName', [Product ID], '<Unknown>')
										AS		[Supplier Name (Fact)],
										
	ApplyMap('Map_ProductID_ProductName', [Product ID], '<Unknown>')
										AS		[Product Name (Fact)],																		
	[Order ID], 
	[Courier Website], 
	[Customer ID]							AS		[Customer ID (Fact)], 	
	[Employee ID]							AS		[Employee ID (Fact)], 		
	[Order Amount], 
	[Order Date]							AS		[Order Date (Fact)], 
	[Payment Received], 
	PO#, 
	[Required Date], 
	[Ship Date], 
	[Ship Via], 
	Shipped, 
	[Product ID]							AS		[Product ID (Fact)], 
	Quantity, 
	[Unit Price], 
	LineSalesAmount
FROM [$(vRootPath)QlikView Security Masterclass\QVD\Orders.qvd] (qvd);

///$tab FACT - Inventory
Inventory:
LOAD 
	[Inventory Date], 
	[Product ID], 
	[Units In Stock], 
	[# Delta Units in Stock], 
	[# Delta Cumulative Units in Stock]
FROM [$(vRootPath)QlikView Security Masterclass\QVD\Inventory.qvd] (qvd);





///$tab Auth Table
//=======================================================
NoConcatenate

All_Combinations_Temp:	
										
LOAD 
	'* | * | *'								AS		%Auth_Master_Key_SA
AutoGenerate 1;

Join

LOAD 
	%Key_FactAuth,
	Upper(%CustomerCountry)				AS		%Auth_Country,
	Upper([Supplier Name (Fact)])			AS		%Auth_SupplierName,
	Upper([Product Name (Fact)])			AS		%Auth_ProductName
Resident Orders;


//=======================================================

NoConcatenate

Country_Combinations_Temp:	
										
LOAD Distinct
	Upper(%COUNTRY)						AS		%Auth_Country
Resident Customers;

Join

LOAD 
	%Key_FactAuth,
	Upper([Supplier Name (Fact)])			AS		%Auth_SupplierName,
	Upper([Product Name (Fact)])			AS		%Auth_ProductName
Resident Orders;

NoConcatenate

Country_Combinations_Temp02:	
LOAD 
	%Key_FactAuth,
	Upper(%CustomerCountry)				AS		%Auth_Country,
	Upper([Supplier Name (Fact)])			AS		%Auth_SupplierName,
	'*'									AS		%Auth_ProductName
Resident Orders;

NoConcatenate

Country_Combinations_Temp03:	
LOAD 
	%Key_FactAuth,
	Upper(%CustomerCountry)				AS		%Auth_Country,
	'*'									AS		%Auth_SupplierName,
	'*'									AS		%Auth_ProductName
Resident Orders;


//=======================================================

NoConcatenate

Supplier_Combinations_Temp:	
LOAD 
	%Key_FactAuth,
	'*'									AS		%Auth_Country,
	Upper([Supplier Name (Fact)])			AS		%Auth_SupplierName,
	Upper([Product Name (Fact)]	)			AS		%Auth_ProductName
Resident Orders;

NoConcatenate

Supplier_Combinations_Temp02:	
LOAD 
	%Key_FactAuth,
	'*'									AS		%Auth_Country,
	Upper([Supplier Name (Fact)])			AS		%Auth_SupplierName,
	'*'									AS		%Auth_ProductName
Resident Orders;


//=======================================================

NoConcatenate

Product_Combinations_Temp:	
										
LOAD 
	%Key_FactAuth,
	'*'									AS		%Auth_Country,
	'*'									AS		%Auth_SupplierName,
	Upper([Product Name (Fact)])			AS		%Auth_ProductName
Resident Orders;

//=======================================================


NoConcatenate

AuthTable:
LOAD *
 Resident All_Combinations_Temp;

Concatenate

LOAD *,
	%Auth_Country & ' | ' & %Auth_SupplierName & ' | ' & %Auth_ProductName
										AS		%Auth_Master_Key_SA
 Resident Country_Combinations_Temp;
 
 Concatenate

LOAD *,
	%Auth_Country & ' | ' & %Auth_SupplierName & ' | ' & %Auth_ProductName
										AS		%Auth_Master_Key_SA
 Resident Country_Combinations_Temp02;

 Concatenate

LOAD *,
	%Auth_Country & ' | ' & %Auth_SupplierName & ' | ' & %Auth_ProductName
										AS		%Auth_Master_Key_SA
 Resident Country_Combinations_Temp03;


LOAD *,
	%Auth_Country & ' | ' & %Auth_SupplierName & ' | ' & %Auth_ProductName
										AS		%Auth_Master_Key_SA
Resident Supplier_Combinations_Temp;

Concatenate

LOAD *,
	%Auth_Country & ' | ' & %Auth_SupplierName & ' | ' & %Auth_ProductName
										AS		%Auth_Master_Key_SA
Resident Supplier_Combinations_Temp02;

Concatenate

LOAD *,
	%Auth_Country & ' | ' & %Auth_SupplierName & ' | ' & %Auth_ProductName
										AS		%Auth_Master_Key_SA
Resident Product_Combinations_Temp;

Drop Tables  All_Combinations_Temp, Country_Combinations_Temp, Country_Combinations_Temp02, Country_Combinations_Temp03, Supplier_Combinations_Temp,  Supplier_Combinations_Temp02, Product_Combinations_Temp;

//=======================================================
///$tab SA ETL
//=======================================================

Temp_SectionAccess:
LOAD 
	Upper(Access)						AS			%SA_ACCESS, 
	Upper(User)						AS			%SA_USERID, 
	Upper(Password)					AS			%SA_PASSWORD, 
	Upper(Country)					AS			%SA_COUNTRY, 
	Upper([Supplier Name])				AS			%SA_SUPPLIERNAME, 
	Upper([Product Name])				AS			%SA_PRODUCTNAME, 
	
	Upper(Country) & ' | ' & Upper([Supplier Name]) & ' | ' & Upper([Product Name])
									AS			%Auth_Master_Key_SA
FROM
[$(vRootPath)QlikView Security Masterclass\METADATA\Challenge_03_MultiField_SA.xls]
(biff, embedded labels, table is Challenge_03_MultiField_SA$);

//=======================================================
NoConcatenate

Full_Access_SectionAccess:
LOAD
	%SA_ACCESS, 
	%SA_USERID, 
	%SA_PASSWORD,
	%SA_COUNTRY, 
	%SA_SUPPLIERNAME, 
	%SA_PRODUCTNAME,	
	'Full Access'						AS			%SA_ReductionFieldName
Resident Temp_SectionAccess
Where  SubStringCount(%Auth_Master_Key_SA, '*') = 3;

Join

LOAD Distinct
	%Auth_Master_Key_SA
Resident AuthTable;


//=======================================================
NoConcatenate

Country_Access_SectionAccess:
LOAD
	%SA_ACCESS, 
	%SA_USERID, 
	%SA_PASSWORD,
	%SA_COUNTRY, 
	%SA_SUPPLIERNAME, 
	%SA_PRODUCTNAME,	
	%Auth_Master_Key_SA,
	'Country'							AS			%SA_ReductionFieldName
Resident Temp_SectionAccess
Where  SubStringCount(%Auth_Master_Key_SA, '*') = 2 ;

Left Join

LOAD Distinct
	%Auth_Master_Key_SA
Resident AuthTable;

//=======================================================
NoConcatenate

SupplierName_Access_SectionAccess:
LOAD
	%SA_ACCESS, 
	%SA_USERID, 
	%SA_PASSWORD,
	%SA_COUNTRY, 
	%SA_SUPPLIERNAME, 
	%SA_PRODUCTNAME,	
	%Auth_Master_Key_SA,
	'Supplier Name'					AS			%SA_ReductionFieldName
Resident Temp_SectionAccess
Where  SubStringCount(%Auth_Master_Key_SA, '*') = 1 ;

Left Join

LOAD Distinct
	%Auth_Master_Key_SA
Resident AuthTable;

//=======================================================

NoConcatenate

ProductName_Access_SectionAccess:
LOAD
	%SA_ACCESS, 
	%SA_USERID, 
	%SA_PASSWORD,
	%SA_COUNTRY, 
	%SA_SUPPLIERNAME, 
	%SA_PRODUCTNAME,	
	%Auth_Master_Key_SA,
	'Product Name'					AS			%SA_ReductionFieldName
Resident Temp_SectionAccess
Where  SubStringCount(%Auth_Master_Key_SA, '*') = 2 AND %SA_PRODUCTNAME <> '*';

Left Join

LOAD Distinct
	%Auth_Master_Key_SA
Resident AuthTable;

//=======================================================

NoConcatenate

Final_SectionAccess:

LOAD * Resident Full_Access_SectionAccess;

Concatenate

LOAD * Resident Country_Access_SectionAccess;

Concatenate

LOAD * Resident SupplierName_Access_SectionAccess;

Concatenate

LOAD * Resident ProductName_Access_SectionAccess;


//=======================================================

Drop Tables Temp_SectionAccess, Full_Access_SectionAccess, Country_Access_SectionAccess, SupplierName_Access_SectionAccess, ProductName_Access_SectionAccess;

//=======================================================
///$tab Section Access
SECTION Access;

LOAD 
	%SA_ACCESS					AS			ACCESS, 
	%SA_USERID					AS			USERID, 
	%SA_PASSWORD				AS			PASSWORD,	 
	%Auth_Master_Key_SA
Resident Final_SectionAccess;

SECTION Application;
///$tab <<<<< EXIT SCRIPT >>>>>
EXIT SCRIPT;
///$tab Dim - Suppliers
Suppliers:
LOAD 
	[Supplier ID]							AS		%Key_SupplierID,		
	[Supplier Name],
	Upper([Supplier Name])				AS		%SUPPLIERNAME
FROM [$(vRootPath)QlikView Security Masterclass\QVD\Supplier.qvd] (qvd);

